buildscript{
    repositories{
        mavenLocal()
        mavenCentral()
        google()
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies{
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

apply plugin: "com.android.application"

configurations{ natives }

repositories{
    mavenCentral()
    maven{ url "https://maven.google.com" }
}

android {
    namespace "caliniya.armavoke"
    buildToolsVersion "34.0.0"
    compileSdkVersion 34
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['../assets']
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        exclude 'META-INF/robovm/ios/robovm.xml'
    }
    defaultConfig {
        multiDexEnabled false
        applicationId "caliniya.armavoke"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version
}
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        dev{
            initWith debug
            minifyEnabled false
        }
        debug{
            minifyEnabled false
        }
    }
}

dependencies{
        implementation project(":core")
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
        
        // Arc Android 特定模块
        implementation "com.github.Anuken.Arc:backend-android:$arcVersion"
        natives "com.github.Anuken.Arc:natives-android:$arcVersion"
        natives "com.github.Anuken.Arc:natives-freetype-android:$arcVersion"
        
        // 崩溃处理库
        implementation 'cat.ereza:customactivityoncrash:2.1.0'
}

// 复制原生库到libs目录
task copyAndroidNatives {
    doFirst {
        file("libs/").mkdirs()
        configurations.natives.files.each { jar ->
            copy {
                from zipTree(jar)
                into file("libs/")
                include "**/*.so"
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("merge") && task.name.contains("JniLibFolders")) {
        task.dependsOn 'copyAndroidNatives'
    }
}

tasks.register('run', Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'caliniya.armadax/caliniya.armadax.AndroidLauncher'
}

// 在 minifyDebugWithR8 任务之前检查
tasks.whenTaskAdded { task ->
    if (task.name == "minifyDebugWithR8") {
        task.doFirst {
            def baseJar = file("$buildDir/intermediates/merged_java_res/debug/base.jar")
            if (!baseJar.exists()) {
                println "ERROR: base.jar does not exist before minify!"
                println "Current directory contents:"
                baseJar.parentFile.listFiles()?.each { f ->
                    println "  - ${f.name} (${f.length()} bytes)"
                }
                
                // 尝试重新生成
                println "Attempting to run mergeDebugJavaResource..."
                tasks.getByName("mergeDebugJavaResource").execute()
            }
        }
    }
}

eclipse.project.name = appName + "-android"